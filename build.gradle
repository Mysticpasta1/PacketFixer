plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'idea'
    id 'eclipse'
}

group = 'net.tonimatasdev'
version = mod_version + "-" + minecraft_version

base {
    archivesName = 'packetfixer'
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                packetfixer {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', "packetfixer"
        }

        server {
            property 'forge.enabledGameTestNamespaces', "packetfixer"
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', "packetfixer"
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', "packetfixer", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "packetfixer.refmap.json"
    config "packetfixer.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {

}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}



tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version_range: minecraft_version_range, forge_version_range: forge_version_range, mod_version: mod_version]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}


tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title": "PacketFixerForge",
                "Specification-Vendor": "TonimatasDEV",
                "Specification-Version": "1",
                "Implementation-Title": "PacketFixerForge",
                "Implementation-Version": project.jar.archiveVersion,
                "Implementation-Vendor": "TonimatasDEV",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
